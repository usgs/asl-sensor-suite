apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

version = '1.2.0'

mainClassName = 'asl.sensor.SensorSuite'
// applicationDefaultJvmArgs = ["-Xms64m", "-Xmx30g"]

repositories {
    mavenCentral()
}
dependencies {

    // https://mvnrepository.com/artifact/org.jfree/jfreechart
    compile group: 'org.jfree', name: 'jfreechart', version: '1.0.19'

    // https://mvnrepository.com/artifact/org.codehaus.jtstand/jtstand-core
    compile group: 'org.codehaus.jtstand', name: 'jtstand-core', version: '1.5.13'

    // https://mvnrepository.com/artifact/edu.sc.seis/seisFile
    compile group: 'edu.sc.seis', name: 'seisFile', version: '1.7.3'

    // https://mvnrepository.com/artifact/org.apache.pdfbox/pdfbox
    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.4'

    // https://mvnrepository.com/artifact/edu.sc.seis/seedCodec
    compile group: 'edu.sc.seis', name: 'seedCodec', version: '1.0.11'

    // https://mvnrepository.com/artifact/org.apache.commons/commons-math3
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.+'

    // https://mvnrepository.com/artifact/commons-logging/commons-logging
    compile group: 'commons-logging', name: 'commons-logging', version: '1.2'

    // https://mvnrepository.com/artifact/net.sf.py4j/py4j/0.8.1
    compile group: 'net.sf.py4j', name: 'py4j', version: '0.8.1'

    // https://mvnrepository.com/artifact/uk.me.berndporr/iirj
    compile group: 'uk.me.berndporr', name: 'iirj', version: '1.0'

    // testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testCompile group: 'junit', name: 'junit', version: '4.+'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    testCompile group: 'commons-io', name: 'commons-io', version: '2.4'
}

task jarMain(type: Jar) {
    baseName = rootProject.name
    manifest {
        attributes 'Implementation-Title': 'ASL Sensor Test Suite',
                'Implementation-Version': version,
                'Main-Class': mainClassName
    }

    /* Adds all dependent libraries*/
    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}

task jarServer(type: Jar) {
    baseName = 'CalServer'
    manifest {
        attributes 'Implementation-Title': 'Calibration Processing Server(cmd)',
                'Implementation-Version': version,
                'Main-Class': 'asl.sensor.CalProcessingServer'
    }

    from {
        configurations.compile.collect {
            it.isDirectory() ? it : zipTree(it)
        }
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
    with jar
}


test {
    maxHeapSize = "4096m"
    maxParallelForks = 4;
    testLogging {
        events "skipped", "failed"
        afterSuite { desc, result ->
            if (!desc.parent) {
                println "Result: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
            }
        }
    }
}

/*Turn off doclint since it is far too strict and breaks the javadoc every time*/
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PRIVATE
}

compileJava {
    options.incremental = true
}

task copyJar(type: Copy) {
    from jarMain
    into rootDir
}


task copyServerJar(type: Copy) {
    from jarServer
    into rootDir
}


build.dependsOn copyJar, copyServerJar
